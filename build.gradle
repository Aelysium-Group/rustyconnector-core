buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'

group = project.maven_group
version = project.version

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = "https://repo.maven.apache.org/maven2/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "https://maven.mrnavastar.rock/" }
    maven { url = "https://repo.eclipse.org/content/groups/releases/" }
}

dependencies {
    shadow "net.kyori:adventure-text-minimessage:4.14.0"
    shadow "com.google.code.gson:gson:2.9.0"
    implementation 'org.reflections:reflections:0.10.2'

    implementation 'org.spongepowered:configurate-core:4.0.0'
    implementation 'org.spongepowered:configurate-yaml:4.0.0'
    implementation "io.javalin:javalin:6.2.0" // REST API
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.0" // API HTTP response code exceptions
    implementation "org.java-websocket:Java-WebSocket:1.5.2"
}

processResources {
    def props = ['version': version, 'config_version': config_version, 'magic_link_version': magic_link_version]
    inputs.properties props
    filteringCharset "UTF-8"

    filesMatching("metadata.json") {
        expand props
    }
}

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = 'UTF-8'
}

shadowJar {
    mergeServiceFiles()
    relocate 'org.reflections', 'group.aelysium.rustyconnector.shaded.org.reflections'
}