buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.github.goooler.shadow:shadow-gradle-plugin:8.1.8'
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'io.github.goooler.shadow'

group = project.maven_group
version = project.version

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = "https://repo.maven.apache.org/maven2/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "https://maven.mrnavastar.me/releases/" }
    maven { url = "https://repo.eclipse.org/content/groups/releases/" }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.1.0'

    implementation "group.aelysium:ara:1.3.2"

    implementation 'com.aventrix.jnanoid:jnanoid:2.0.0'
    implementation 'net.kyori:adventure-api:4.17.0'
    implementation 'com.google.code.gson:gson:2.11.0'

    implementation "io.javalin:javalin:6.3.0" // REST API
    implementation "com.fasterxml.jackson.core:jackson-databind:2.17.0" // API HTTP response code exceptions
    implementation "org.java-websocket:Java-WebSocket:1.5.7"
}

processResources {
    def props = ['version': version, 'config_version': config_version, 'magic_link_version': magic_link_version]
    inputs.properties props
    filteringCharset "UTF-8"

    filesMatching("metadata.json") {
        expand props
    }
}

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set('rustyconnector-core')
    archiveClassifier.set('')
    mergeServiceFiles()
    relocate 'org.reflections', 'group.aelysium.rustyconnector.shaded.org.reflections'
}

tasks.named('jar').configure {
    enabled = false
}
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xdoclint:none']
}
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar.finalizedBy(shadowJar)